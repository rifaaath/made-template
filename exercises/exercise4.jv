pipeline TemperaturePipeline {
    TempExtractor ->
        TempZIPInterpreter;
    
    TempZIPInterpreter
        -> TempCSVPicker
        -> TempFileInterpreter
        -> TempCSVInterpreter
        -> TempHeaderWriter
        -> TempTableInterpreter
        -> TempConverter1
        -> TempConverter2
        -> TempLoader;

    
    // block TempExtractor oftype LocalFileExtractor{
    //     filePath: "mow.zip";
    // }
    block TempExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block TempZIPInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block TempCSVPicker oftype FilePicker{
        path: "./data.csv";
    }

    block TempFileInterpreter oftype TextFileInterpreter{}

    block TempCSVInterpreter oftype CSVInterpreter{
        delimiter: ";";
    }

    block TempHeaderWriter oftype CellWriter {
        at: range A1:J1;
        write: ["id", "producer", "model", "month", "temperature", "", "", "", "", "battery_temperature"];
    }

    block TempTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype valid_id,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype valid_month,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    valuetype valid_id oftype integer{
        constraints: [constraint_id];
    }
    constraint constraint_id on integer:
        value>0;

    valuetype valid_month oftype integer{
        constraints: [constraint_month];
    }
    constraint constraint_month on integer:
        value >0 and value <13;

    transform CelsiusToFahrenheit {
        from Celsius oftype decimal;
        to Fahrenheit oftype decimal;

        Fahrenheit: (Celsius * 9/5) + 32;
    }

    block TempConverter1 oftype TableTransformer {
        inputColumns: ['temperature'];
        outputColumn: 'temperature';
        uses: CelsiusToFahrenheit;
    }

    block TempConverter2 oftype TableTransformer {
        inputColumns: ['battery_temperature'];
        outputColumn: 'battery_temperature';
        uses: CelsiusToFahrenheit;
    }

    block TempLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "temperatures.sqlite";
    }
}